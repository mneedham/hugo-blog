+++
draft = false
date="2015-05-19 22:45:48"
title="Neo4j: Finding all shortest paths"
tag=['neo4j', 'cypher']
category=['neo4j']
+++

One of the Cypher language features we show in Neo4j training courses is the http://neo4j.com/docs/milestone/query-match.html#_shortest_path[shortest path function] which allows you to find the shortest path in terms of number of relationships between two nodes.

Using the movie graph, which you can import via the ':play movies' command in the browser, we'll first create a 'KNOWS' relationship between any people that have appeared in the same movie:

[source,cypher]
----

MATCH (p1:Person)-[:ACTED_IN]->()<-[:ACTED_IN]-(p2:Person)
MERGE (p1)-[:KNOWS]-(p2)
----

Now that we've got that relationship we can easily find the shortest path between two people, say Tom Cruise and Tom Hanks:

[source,cypher]
----

MATCH (p1:Person {name: "Tom Hanks"}), (p2:Person {name: "Tom Cruise"}),
      path = shortestpath((p1)-[:KNOWS*]-(p2))
RETURN path
----

image::{{<siteurl>}}/uploads/2015/05/graph-18.png[Graph  18,300]

That works pretty well but what if we want to find the longest shortest path between any two people in the graph? We can calculate it like this:

[source,cypher]
----

MATCH (p1:Person), (p2:Person),
      path = shortestpath((p1)-[:KNOWS*]-(p2))
RETURN path
ORDER BY LENGTH(path) DESC
LIMIT 1
----

image::{{<siteurl>}}/uploads/2015/05/graph-19.png[Graph  19,500]

So that's 6 hops which is actually the http://en.wikipedia.org/wiki/Six_Degrees_of_Kevin_Bacon[Bacon number] - I expect we'd probably see a smaller maximum value if we imported all the movies.

And to round off the post what if we want to find the longest shortest path between the 10 people who acted in the most movies? We might start out with the following query which seems like it should do the job:

[source,cypher]
----

MATCH (p1:Person)-[:ACTED_IN]->()

WITH p1, COUNT(*) AS appearances
ORDER BY appearances DESC
LIMIT 10

WITH p1 AS p1, p1 AS p2
MATCH path = shortestpath((p1)-[:KNOWS*]-(p2))
RETURN path
ORDER BY LENGTH(path) DESC
LIMIT 1
----

*Unfortunately if we run that query we get no rows returned because 'p1' and 'p2' always refer to the same node.*

Instead we can calculate the shortest path between our hardest working people by creating a cross product using COLLECT and UNWIND:

[source,cypher]
----

MATCH (p1:Person)-[:ACTED_IN]->()

WITH p1, COUNT(*) AS appearances
ORDER BY appearances DESC
LIMIT 10

WITH COLLECT(p1) AS ps
UNWIND ps AS p1 UNWIND ps AS p2
MATCH path = shortestpath((p1)-[:KNOWS*]-(p2))
RETURN path
ORDER BY LENGTH(path) DESC
LIMIT 1
----

image::{{<siteurl>}}/uploads/2015/05/graph-20.png[Graph  20,400]

That's all for now!
